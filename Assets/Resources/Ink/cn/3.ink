INCLUDE common.ink

-> chapter_3

=== chapter_3 ===

-
  * “这个是……？”
-

“这是可视电话喔。”
 // 屏幕上突然出现女主的全息投影

-
  * “哇！” 
-

“没见过吧？这个你们 1988 年肯定没有。”
“虽然最开始的可视电话也不是这样的，不过也没差啦。”
“这样一来，你就不用手打莫尔斯电码和我通讯了，直接说话就好啦。”
“而且还可以随身携带。怎么样，是不是更加相信我是 2088 年的人了呢！”

-
  * “的确[……”]不过你确定不是因为你懒得打通讯了？”
-

“……我在你眼中到底是个什么样的形象啊！”
“不过它会需要充电来着。你有发电机吗？”

-
  * “有倒是有[……”]，毕竟航标灯是用柴油发电机驱动的。但柴油发电机在地下一层的房间里。”
    ** “然而钥匙……”
-

“钥匙弄丢了？听上去真不像你的风格。”

-
  * “我已经叫了备用钥匙。[”]不过他们得从雷克雅未克过来，再加上这两天天气不太好，估计他们被困在路上了。”
    ** “不过没关系，上次加注的柴油到现在应该还够再烧半天的。”
-

“嗨呀，这有什么。你早说我刚刚就把万能钥匙带过来了呀。”

-
  * “……居然还有这种方便的发明。”
-

“哼，最不方便的就是我还要再跑一趟！”
“你等着，我这就给你送过来。快想想还有没有什么需要的，尽管把我当作哆啦A梦就好啦！”

-
  * “哆啦A梦？”
    “哎呀，1988 年还没有哆啦A梦吗？还是你没看过？”
    “总之是我无所不能的意思！快来好好地依赖我吧！”
    ** “谢谢，不必了。”
      “真是冷淡……”
  * “应该，没有了。”
    “不要应该呀，真是的。”
-

“那么你好好等着喔。我把万能钥匙送过来。到时候记得好好犒劳我一顿！”
（通讯中断）

-
  * [（继续故事）]-> chapter_3_after
-

-> DONE

=== chapter_3_after ===

“磷光！怎么样，拿到了吗？”

-
  * “啊啊，拿到了。[”]谢谢。”
    “不客气！快去试试能不能开吧。照理应该是小菜——”
  * “没有。”
    “欸——？不会吧？我明明有好好放在操作台上的来着……”
    ** “骗你的，拿到了。”
      “……不理你了喔？”
      “嘛……快去试试能不能开吧。照理应该是小菜——”
-

（嘶啦嘶啦）

-
  * “……‘薄明’？”
  * “……X05F1822？”
-

（无线电静默）

-
  * “TF090 呼叫 X05F1822！收到请回答！收到请回答！”
-

（无线电静默）

-> chapter_3_unlit

= chapter_3_unlit
-
  * [“……不知道信号灯能不能……！”]（前往控制台）
  * [“……回去试试无线电报机……！”]（前往通讯室）
  + “重复！TF090 呼叫 X05F1822！[”]收到请回答！收到请回答！”
    （无线电静默）
    -> chapter_3_unlit
-

-
  * “……该死，停电了[……”]！明明上次加满的，是因为冬天耗油多吗……”
-

-
  * （前往地下一层）
-

-
  * （找到柴油，加注，重新启动柴油发电机）
-

（嘶啦嘶啦）
“……磷光！薄明呼叫磷光！收到请回答！收到请回答！！”

-
  * “磷光收到。[”]好久不见，薄—”
    “磷光！你终于！……天哪，快吓死我了。你刚刚突然就没声了，航标灯也不见了。我以为你突然消失了……”
    ** “别怕，我不是在这儿吗。”
      “嗯……你还在。太好了……”
      “欸，你刚刚是不是叫了我薄明？”
      *** “没有，你听错了。”
        “骗人！我不可能听错！以后你都直接叫我薄明好不好？”
        -> chapter_3_name
      *** “是啊，怎么了？”
        “好耶！以后你都直接叫我薄明好不好？”
        -> chapter_3_name
  * （沉默）
    “薄明呼叫磷光！薄明呼叫……欸，航标灯亮起来了！”
    “太好了……我还以为……”
    ** “以为什么？”
      “呜啊！你，你不要突然吓人好不好！”
      *** “所以以为什么？”
        “你……什么都没有！”
-

-> chapter_3_lit_again

= chapter_3_name
-
* “为什么？”
  “我说，磷光。你真的很不懂情调欸。”
  “呼号多冷冰冰啊。我们都已经是患难之交了，就不能直接称呼名字吗！”
  ** “……行吧。”
    “好不情愿！哼哼，不过还是我赢了！”
* “好啊。”
  “呜欸，好干脆。你真的是磷光？”
  ** “如假包换。”
    “好吧……反正你肯定会说‘名字只是另一种代号而已’这样的话吧。”
    “嘛，总之还是我赢了！”
-

“那么以后就请多多指教啦，磷光！”

-
  * “啊啊，请多指教，薄明。”
-

-> chapter_3_lit_again

= chapter_3_lit_again

“说起来，为什么会突然就没讯号了呢？”

-
  * “我倒是有个猜想。[”]在与你的通话突然中断的时候，正好所有灯都灭了。”
    ** “我去检查后发现是停电造成的[。”]，于是立刻赶去重新启动了柴油发电机。然后突然你的信号就进来了。”
-

“啊，所以我的万能钥匙好用？”

-
  * “为什么你的关注点这么奇怪……[”]是的，很好用。”
-

“嘿嘿，能帮上你就好。”
“啊，是不是跑题了？我们在说什么来着？”

-
  * “是，而且是你亲手带跑的[。”]……无所谓了。我们刚刚在说为什么信号会突然中断。”
-

“啊，对，是因为停电？”

-
  * “我认为停电是直接原因，但可能不是本质原因。”
    ** “如果你乐意的话，我们可以做个实验……”
-

“不要。”

-
  * “欸。”
-

“不要。”

-
  * “我还什么都没说……”
-

“绝对不要！我不想再失去与你的联系了。”

-
  * “好吧，总之我猜，有一种可能是因为航标灯。”
    ** “之前我都没有关闭过航标灯。而无论是电报机还是信号灯我都开关过。”
      *** “此外，你我之间的物资交换也发生在灯笼房[……”]。这是你唯一能前往的地方，也是我能去到的最高处。”
        **** “综上，我想只要保持航标灯长明，我们的连接就不会断开。”
-

“……嗯，有道理。脑子很好用嘛，磷光！”
“你真的是 1988 年的人吗？感觉你可以轻松活在我这个时代诶。”

-
  * “你要不潜个水下来看看我还活着没有？”
-

“不不不，再怎么说也不可能吧……不可能吧？”
“要不我明天试试？”

-
  * “别，这太危险了。[”]而且到时候要是找到我的尸体就不好了。”
-

“别说这么不吉利的话！”
“啊……放松下来突然觉得好累。今天真的发生了好多事……”
“我要去休息啦，你也早点休息！”
“……不许关航标灯喔？”

-
  * “本来晚上也要一直开着啦……”
-

“就算是白天也不许关！”
“那就这样。晚安……磷光。”

-
  * “嗯。晚安，薄明。”
-

-
  * [（关闭可视电话）]
-

（无线电静默）

-> END
